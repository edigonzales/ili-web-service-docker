plugins {
    id 'java'
}

apply from: "$rootDir/gradle/versioning.gradle"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    dwnld {
        transitive = false
    }
}

dependencies {
    dwnld "io.github.sogis:ili2c-web-service:5.6.3-14:plain@war"
}

tasks.register('copyDependencies', Copy) {
    from configurations.dwnld
    into "$buildDir/dwnld"
}

tasks.register('buildAndPushMultiArchImage', Exec) {
    dependsOn 'copyDependencies'
    def githash = getCheckedOutGitCommitHash()
    def build_timestamp = getTimestamp()

    // TODO add ghcr.io 
    commandLine 'docker', 'buildx', 'build',
                '--platform', 'linux/amd64,linux/arm64',
                '-t', "sogis/ili2c-web-service:0",
                '-t', "sogis/ili2c-web-service:0.0",
                '-t', "sogis/ili2c-web-service:0.0.$buildNumber",
                '-t', "sogis/ili2c-web-service:latest",
                '--label', "ili2c-web-service.created=$build_timestamp", 
                '--label', "ili2c-web-service.git_commit=$githash",
                '--label', "ili2c-web-service.version=$version",
                //'-f', 'Dockerfile',  '.', '--push'
                '-f', 'Dockerfile',  '.'
}

def getCheckedOutGitCommitHash() {
    'git log -1 --pretty=%H'.execute().text.trim()
}

def getTimestamp() {
    def date = new Date()
    return date.format('yyyy-MM-dd HH:mm:ss')
}
